/**
 * @fileOverview Matchers and running regular expressions.
 */
package (
    notToken,
    
// Characters
    character,
    characteri,
    characterRange,
    characterRangei,
    anyCharacter,
    
// Character Classes
    digit,
    nonDigit,
    space,
    nonSpace,
    word,
    nonWord,
    
// Assertions
    assert,
    assertNot,
    wordBoundary,
    notWordBoundary,
    bof,
    bol,
    eof,
    eol,

// 
    nothing,
    anys,
    any,
    chains,
    
// Quantifiers
    between,
    betweenNonGreedy,
    atMost,
    
// Group
    group,
    backReference,
    
// Running
    matchStream,
    match,
    
    execStream,
    exec)
with
    import 'amulet/record' record,
    import 'parse/parse' parse {
        always,
        attempt,
        bind,
        choice,
        choices,
        either,
        enumerations,
        getState,
        fail,
        lookahead,
        modifyState,
        next,
        token},
    import 'parse/lang' {betweenTimes, times},
    import 'parse/text' text,
    import 'nu/stream' stream {isEmpty, foldl, map, rest, 'end': NIL},
    import 'nu/gen' {range}
in {

var toLowerCase =  Function.prototype.call.bind(String.prototype.toLowerCase);
var toUpperCase =  Function.prototype.call.bind(String.prototype.toUpperCase);
var indexOf = Function.prototype.call.bind(Array.prototype.indexOf);

var args = \args(...) -> args;

var join = (foldl, (+), '');

var copy = \arr{length} -> {
    var out = [];
    for (var i = 0; i < length; i = i + 1)
        out[i] = arr[i];
    return out;
};

var contains = indexOf \>> (!==, -1);

var has = \o, i -> o[i] !== undefined;

var fromCharCode = \x -> String.fromCharCode(x);

var isLineTerminator = let
    lineTerminators = '\u2029\u2028\u000D\u000A'
in
    (contains, lineTerminators);

var isWordChar = let
    wordChars = "abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789_"
in
    (contains, wordChars);

/* State
 ******************************************************************************/
/**
 * Parser State used for matching.
 * 
 * Keeps track of the previously consumed character.
 */
var State = record.declare(new parse.ParserState, [
    'input',
    'position',
    'userState',
    'previous']);

State.prototype.next = \x -> {
    if (!this._next) {
        var s = State.create(
            rest(this.input),
            this.position.increment(x),
            this.userState,
            x);
        this._next = \_, m, cok -> cok(x, s, m);
    }
    return this._next;
};

/* Data
 ******************************************************************************/
/**
 * User data threaded though parsers.
 */
var Data = record.declare(null, [
    'groups',
    'captures']);

Data.setCapture = \s{captures}, i, x -> {
    var c = copy(captures);
    c[i] = x;
    return Data.setCaptures(s, c);
};

/* Base Parsers
 ******************************************************************************/
var empty = always('');

var fromCharCodeParser = join
    \> \x -> parseInt(x, 16)
    \> String.fromCharCode
    \> always;

var joinP = \p -> bind(p, always <\ join);

var previous = parse.extract(\x -> x.previous);

var setCapture = \i, x ->
    modifyState(\s -> Data.setCapture(s, i, x));

/* Assertions
 ******************************************************************************/
var test = \p, f ->
    bind(p, \x -> f(x) ? empty : fail());

var not = \m -> 
    either(
        next(m, fail()),
        always());

/**
 * Check the input matches a parser without consuming any input.
 */
assert = \p ->
    next(
        lookahead <| p,
        empty);

/**
 * Check the input does not match a parser without consuming any input.
 */
assertNot = not \> assert;

/**
 * Match the beginning of a file. Does not consume any input.
 */
bof = test(parse.getPosition, \pos -> pos.index === 0);

/**
 * Match the beginning of a line. Does not consume any input.
 */
bol = either(
    bof,
    test(previous, isLineTerminator));

/**
 * Match the end of a file. Does not consume any input.
 */
eof = assert <| parse.eof;

/**
 * Match the end of a line. Does not consume any input.
 */
eol = either(
    eof,
    assert <| token(isLineTerminator));

/**
 * Match a word boundary. Does not consume any input.
 */
wordBoundary = either(
    eof,
    assert <| bind(previous, \p ->
        token(\c -> isWordChar(c) && !isWordChar(p))));

/**
 * Does not match a word boundary. Does not consume any input.
 */
notWordBoundary = not <| wordBoundary;

/* Groups
 ******************************************************************************/
/**
 * Create a new group with name `i` for matcher `p`.
 * 
 * `p` is run and result is stored in the in state for later and as part of the
 * result.
 */
group = \p, i ->
    next(
        setCapture(i, ''),
        bind(p, \x ->
            next(
                setCapture(i, x),
                always(x))));

/**
 * Match a reference to group `i`.
 * 
 * If the group has already been consumed, matches the text of that capture.
 * Otherwise, do nothing. Non existent groups always fail.
 */
backReference = \i ->
    bind(getState, \{captures, groups} ->
        has(captures, i) ?
            text.string(captures[i]) :
            (has(groups, i) ? empty : fail()));

/* Character Matchers
 ******************************************************************************/
/**
 * Match a single character.
 */
character = text.character;

/**
 * Match a single character case insensitively.
 */
characteri = \c ->
    either(
        character(toLowerCase(c)),
        character(toUpperCase(c)));

/**
 * Match a character in a range.
 */
characterRange =
    \start, end ->
        range(start.charCodeAt(0), end.charCodeAt(0) + 1, 1)
    \>> (map, fromCharCode)
    \>> join
    \>> text.characters;

/**
 * Match a character in a range case insensitively.
 */
characterRangei = \b, c ->
    choice(
        characterRange(b, c),
        characterRange(toLowerCase(b), toLowerCase(c)),
        characterRange(toUpperCase(b), toUpperCase(c)));

/**
 * Match any character expect a line terminator.
 */
anyCharacter = token((!) <\ isLineTerminator);

/* Character Class Matchers
 ******************************************************************************/
notToken = \p ->
    token((!) <\ (parse.test, p));

/**
 * Match any digit character.
 */
digit = characterRange('0', '9');

/**
 * Match any non digit character.
 */
nonDigit = notToken <| digit;

/**
 * Match any space character
 */
space = text.characters("\u0009\u000b\u000c\u0020\u00a0\ufeff\u000a\u000d\u2028\u2029");

/**
 * Match any non space character.
 */
nonSpace = notToken <| space;

/**
 * Match any word character.
 */
word = text.characters("abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789_");

/**
 * Match any non word character.
 */
nonWord = notToken <| word;

/* Parsers
 ******************************************************************************/
/**
 * Matches nothing.
 */
nothing = fail();

/**
 * Match the first of 1 or more patterns.
 * 
 * @param s Stream of patterns.
 */
anys = (map, attempt) \> choices;

/**
 * Match the first of 1 or more patterns.
 */
any = args \>> stream.from \>> anys;

chains = enumerations \> joinP;

/* Quantifiers
 ******************************************************************************/
/**
 * Match `p` at most `max` times.
 * 
 * Unlike the regular `parse.js` parsers, this is locally non-greedy but will
 * attempt to consume as little input as possible to make the entire parsing
 * valid.
 * 
 * @TODO: pretty damn ugly. 
 */
atMost = \max, p ->
    (max === 0 ? always(NIL) :
        \state, m, cok, cerr, eok, eerr -> 
            let r = parse.trampoline <| eok(NIL, state, m) in
                (r ? r :
                    parse.cons(p, atMost(max, p))(state, m, cok, cerr, eok, eerr)));

/**
 * Greedily match a parser between `min` and `max` times.
 */
between = betweenTimes \>> joinP;

/**
 * Non-Greedily match a parser between `min` and `max` times.
 * 
 * @see atMost for description of how non-greedy matching works.
 */
betweenNonGreedy = joinP <<\ \min, max, p ->
    parse.append(
        times(min, p),
        atMost(max - min, p));

/* Running
 ******************************************************************************/
/**
 * Run `pattern` against input once and return the result.
 * 
 * @pattern Pattern from `re`
 * @param input Stream of input to run `pattern` against.
 * 
 * @return Either the set of captures or null if none.
 */
matchStream = \{pattern, groups}, input ->
    parse.parseState(
        pattern,
        State.create(
            input,
            parse.Position.initial,
            Data.create(groups, new Array(groups.length)),
            null),
        \_, s -> s.userState.captures,
        \ -> null);

/**
 * @see matchStream
 * 
 * @param input Array like of characters.
 */
match = \pattern, input ->
    matchStream(
        pattern,
        stream.from(input));

/**
 * Run `pattern` against input incrementally until a match is found.
 * 
 * @param pattern Pattern
 * @param input Stream .
 */
execStream = \pattern, input -> {
    var result;
    var feed = input;
    do {
        result = matchStream(pattern, feed);
        if (isEmpty(feed))
            break;
        feed = rest(feed);
    } while (!result);
    return result;
};

/**
 * @see execStream
 * 
 * @param input Array like of characters.
 */
exec = \pattern, input ->
    execStream(
        pattern,
        stream.from(input));
}