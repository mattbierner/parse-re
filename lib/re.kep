/**
 * @fileOverview Regular expression grammar parsers.
 */
package (
    pattern,
    
// Flags
    RE_NONE,
    RE_I,
    RE_G,
    RE_M,
    
// Evaluation
    evaluate,
    evaluateStream)
with
    import 'amulet/record' record,
    import 'parse/parse' parse {
        always,
        attempt,
        bind,
        binds,
        choice,
        either,
        enumeration,
        fail,
        getState,
        many,
        many1,
        modifyState,
        next,
        optional,
        rec,
        test,
        token},
    import 'parse/lang' {between, sepBy, sepEndBy, times, then},
    import 'parse/text' {anyChar, digit, character, characters, string},
    import 'nu/stream' stream {foldl},
    import 'nu/gen' gen,
    import 'parse_re/match' match
in {

/* Forward
 ******************************************************************************/
var disjunction = \args(...) -> disjunction.apply(null, args);

var classRanges = \args(...) -> classRanges.apply(null, args);

/*
 ******************************************************************************/
var identity = \x -> x;

var constant = \x -> \() -> x;

var join = (foldl, (+), '');

var copy = \arr{length} -> {
    var out = [];
    for (var i = 0; i < length; i = i + 1)
        out[i] = arr[i];
    return out;
};

var fromCharCodeParser =
    join
    \> \x -> parseInt(x, 16)
    \> String.fromCharCode
    \> always;

var notToken = \p -> 
    token((!) <\ (test, p));
    
/* Data
 ******************************************************************************/
var Data = record.declare(null, [
    'flags',
    'groups']);

var addGroup = \a, g -> {
    var c = copy(a);
    c.push(g);
    return c;
};

var setGroup = \a, i, g -> {
    var c = copy(a);
    c[i] = g;
    return c;
};

Data.addGroup = \s, g ->
    s.setGroups(addGroup(s.groups, g));

Data.setGroup = \s, i, g ->
    s.setGroups(setGroup(s.groups, i, g));

/* Matchers
 ******************************************************************************/
/**
 * Create a matcher for a character.
 * 
 * Looks at flags to determine case sensitivity.
 */
var matchCharacter = \c ->
    bind(getState, \s -> 
        always(s.flags & RE_I ?
            match.characteri(c) :
            match.character(c)));

/**
 * Create a matcher for a character.
 * 
 * Looks at flags to determine case sensitivity.
 */
var matchCharacterFrom = \p ->
    bind(p, matchCharacter);

/**
 * Create a matcher for a character range.
 * 
 * Looks at flags to determine case sensitivity.
 */
var matchCharacterRange = \from, to ->
    bind(getState, \s -> 
        always(s.flags & RE_I ?
            match.characterRangei(from, to) :
            match.characterRange(from, to)));

/**
 * Create a beginning of file matcher.
 * 
 * Looks at flags to determine multiline behavior.
 */
var matchBof = bind(getState, \s -> 
    always(s.flags & RE_M ?
        match.bol :
        match.bof));

/**
 * Create a end of file matcher.
 * 
 * Looks at flags to determine multiline behavior.
 */
var matchEof = bind(getState, \s -> 
    always(s.flags & RE_M ?
        match.eol :
        match.eof));

var group = \p ->
    next(
        modifyState(\s -> Data.addGroup(s, null)),
        bind(getState, \s ->
            let i = s.groups.length - 1 in
                bind(p, \p ->
                    let impl = match.group(p, i) in
                        parse.next(
                            modifyState(\s -> Data.setGroup(s, i, impl)),
                            always(impl)))));

/* General Parsers
 ******************************************************************************/
var decimalDigit = characters('0123456789');

var hexDigit = characters('0123456789abcdefABCDEF');

var decimalDigits = many1(decimalDigit);

var decimalIntegerLiteral = bind(
    decimalDigits,
    join \> parseInt \> always);

var identifierPart = choice(
    characters('abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789$_'),
    digit);

/* Base Escapes
 ******************************************************************************/
/**
 * Character specified as a 2 digit unicode point.
 */
var hexEscapeSequence = next(
    character('x'),
    bind(times(2, hexDigit),
        fromCharCodeParser));

/**
 * Character specified as a 4 digit unicode point.
 */
var unicodeEscapeSequence = next(
    character('u'),
    bind(times(4, hexDigit),
        fromCharCodeParser));

/**
 * Decimal Character.
 */
var decimalEscape = decimalIntegerLiteral;

/**
 * Other escaped characters.
 */
var identityEscape = either(
    characters('\u200C\u200D'),
    notToken(identifierPart));

/**
 * Escape for a special characters
 */
var controlEscape = let
    map = \from, to ->
        next(
            character(from),
            always(to))
in
    choice(
        map('t', '\u0009'),
        map('n', '\u000A'),
        map('v', '\u000B'),
        map('f', '\u000C'),
        map('r', '\u000D'));
 
/**
 * Escapes for a character calculated from a control letter's value.
 */
var controlLetter = bind(
    characters('abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ'),
    \x -> x.charCodeAt(0) % 32
        \> String.fromCharCode
        \> always);

/* Escapes Matchers
 ******************************************************************************/
/**
 * Escapes representing a matcher for a class of characters.
 */
var characterClassEscape = choice(
    next(
        character('d'),
        always(match.digit)),
    
    next(
        character('D'),
        always(match.nonDigit)),
    
    next(
        character('s'),
        always(match.space)),
    
    next(
        character('S'),
        always(match.nonSpace)),
    
    next(
        character('w'),
        always(match.word)),
    
    next(
        character('W'),
        always(match.nonWord)));

/* 
 ******************************************************************************/
/**
 * Escaped character in an atom.
 */
var characterEscape = choice(
    controlEscape,
    next(
        character('c'),
        controlLetter),
    hexEscapeSequence,
    unicodeEscapeSequence,
    identityEscape);

/**
 * Escaped character in a character class.
 */
var classEscape = choice(
    bind(
        decimalEscape,
        always <\ String.fromCharCode),
    next(
        character('b'),
        always('\u0008')),
    characterClassEscape,
    characterEscape);

/* Character Classes
 ******************************************************************************/
/**
 * Correctly handle false ranges where either `start` or `end` is a matcher
 * and not a single character.
 */
var rangeFor = let 
    wrap = \c -> (typeof c === 'function' ? c : character(c))
in
    \start, end ->
        (typeof start === 'function' || typeof end === 'function' ?
            always(match.any(
                wrap(start),
                match.character('-'),
                wrap(end))) :
            matchCharacterRange(start, end));

var classAtomNoDash = let 
    reserved = characters('-]')
in
    either(
        next(
            character('\\'),
            classEscape),
        notToken(reserved));

var classAtom = either(
    character('-'),
    classAtomNoDash);

/**
 * 
 */
var nonEmptyClassRangesNoDash = let
    classRange = binds(
        enumeration(
            then(
                classAtomNoDash,
                character('-')),
            classAtom),
        rangeFor)
in
    rec <| \nonEmptyClassRangesNoDash -> 
        choice(
            attempt <| bind(
                enumeration(
                    classRange,
                    classRanges),
                always <\ match.anys),
            
            attempt <| bind(
                enumeration(
                    matchCharacterFrom(classAtomNoDash),
                    nonEmptyClassRangesNoDash),
                always <\ match.anys),
            
            matchCharacterFrom(classAtom));

var nonEmptyClassRanges = let
    classRange = binds(
        enumeration(
            then(
                classAtom,
                character('-')),
            classAtom),
        rangeFor)
in
    choice(
        attempt <| bind(
            enumeration(
                classRange,
                classRanges),
            always <\ match.anys),
        
        attempt <| bind(
            enumeration(
                matchCharacterFrom(classAtom),
                nonEmptyClassRangesNoDash),
            always <\ match.anys),
        
        matchCharacterFrom(classAtom));

classRanges = optional(match.nothing, nonEmptyClassRanges);

/**
 * Pattern for a matcher that consumes a category of characters.
 */
var characterClass = between(character('['), character(']'),
    either(
        next(
            character('^'), 
            bind(
                classRanges,
                always <\ match.notToken)),
        classRanges));

/* Atom
 ******************************************************************************/
/**
 * Escaped atom character or pattern.
 * 
 * Decimal escapes are treated as back references in atoms.
 */
var atomEscape = choice(
    bind(
        decimalEscape,
        always <\ match.backReference),
    
    matchCharacterFrom(characterEscape),
    
    characterClassEscape);

/**
 * Character in an atom.
 * 
 * Includes any character except a set of reserved ones that have special
 * meaning in regular expressions.
 */
var patternCharacter = let
    reserved = '^$\\.*+?()[]{}|'
in
    matchCharacterFrom(token((!) <\ (test, characters(reserved))));

/**
 * Regular expression consuming matching unit.
 */
var atom = choice(
    patternCharacter,
    
    next(
        character('.'),
        always(match.anyCharacter)),
    
    next(
        character('\\'),
        atomEscape),
    
    characterClass,
    
    between(character('('), character(')'),
        either(
            next(
                string('?:'),
                disjunction),
            group(disjunction))));

/* Assertion
 ******************************************************************************/
/**
 * Regular expression non-consuming matchers
 */
var assertion = choice(
    next(
        character('^'),
        matchBof),
    
    next(
        character('$'),
        matchEof),
    
    next(
        string('\\b'),
        always(match.wordBoundary)),
    
    next(
        string('\\B'),
        always(match.notWordBoundary)),
    
    between(character('('), character(')'),
        bind(
            either(
                next(string('?='), always(match.assert)),
                next(string('?!'), always(match.assertNot))),
            \x -> bind(disjunction, always <\ x))));

/* Term
 ******************************************************************************/
/**
 * 
 */
var quantifierPrefix = choice(
    next(
        character('*'),
        always([0, Infinity])),
    
    next(
        character('+'),
        always([1, Infinity])),
    
    next(
        character('?'),
        always([0, 1])),
    
    between(character('{'), character('}'),
        binds(
            enumeration(
                decimalIntegerLiteral,
                optional(null, character(',')),
                optional(Infinity, decimalIntegerLiteral)),
            \lower, hasUpper, upper ->
                always(hasUpper ?
                    [lower, upper] :
                    [lower, lower]))));

/**
 */
var quantifier = binds(
    enumeration(
        quantifierPrefix,
        optional(false, character('?'))),
    \[min, max], lazy ->
        always(lazy ?
            (match.betweenNonGreedy, min, max) :
            (match.between, min, max)));

/**
 * Top level element making up regular expressions
 */
var term = either(
    attempt <| assertion,
    binds(
        enumeration(
            atom,
            optional(identity, quantifier)),
        \atom, quantifier ->
            always(quantifier(atom))));

/* Pattern
 ******************************************************************************/
var alternative = bind(
    many(term),
    always <\ match.chains);

disjunction = bind(
    sepBy(character('|'),
        alternative),
    always <\ match.anys);

/**
 * Top level parser for a regular expression.
 */
pattern = group(disjunction);

/* Evaluation
 ******************************************************************************/
/**
 * RE_I - Match case insensitively
 * RE_G - Match globally. Has no effect on resulting regexp but on how it is
 *    executed.
 * RE_M - `^` and `$` also match state of lines and end of lines respectively.
 */
RE_NONE = 0;
RE_I = 1 << 0;
RE_G = 1 << 1;
RE_M = 1 << 2;

/**
 * Map input specifying a regular expression to an opaque matcher object
 * encapsulating the behavior of that regular expression.
 * 
 * Errors if the regular expression is invalid.
 * 
 * @param input Stream of characters specifying a regular expression.
 * @param [flags] Flags that control the behavior of the resulting
 *     matcher.
 */
evaluateStream = \input, flags -> let
    patternFlags = (flags || RE_NONE),
    
    ok = \_, {'userState':{groups}} -> ({
        'pattern': groups[0],
        'groups': groups,
        'flags': flags
    }),
    
    err = \x -> { throw x; }
in 
    parse.parseStream(
        pattern,
        input,
        Data.create(patternFlags, []),
        ok,
        err);

/**
 * @see evaluateStream
 * 
 * @param input Array like of characters input.
 */
evaluate = \input, flags ->
    evaluateStream(
        stream.from(input),
        flags);

}